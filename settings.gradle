pluginManagement {
    repositories {
        google {
            content {
                includeGroupByRegex("com\\.android.*")
                includeGroupByRegex("com\\.google.*")
                includeGroupByRegex("androidx.*")
            }
        }
        mavenCentral()
        gradlePluginPortal()
    }
}
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
        maven {
            url = uri("https://gitlab.com/api/v4/projects/77552064/packages/maven")
            credentials(HttpHeaderCredentials) {
                name = "Private-Token"
                // Try to get token from: 1) System property, 2) Environment variable, 3) local.properties
                def token = System.getProperty("GITLAB_READ_TOKEN") ?: System.getenv("GITLAB_READ_TOKEN") ?: ""
                if (!token) {
                    def props = new Properties()
                    def localPropsFile = new File(rootDir, "local.properties")
                    if (localPropsFile.exists()) {
                        localPropsFile.withInputStream { props.load(it) }
                        token = props.getProperty("GITLAB_READ_TOKEN", "")
                    }
                }
                value = token
            }
            authentication {
                create("header", org.gradle.authentication.http.HttpHeaderAuthentication)
            }
        }
    }
}

rootProject.name = "My Application -00"
include ':app'
